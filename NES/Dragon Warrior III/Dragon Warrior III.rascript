// Dragon Warrior III
//  #ID = 1667
//  MD5: 16a03048ce659d3d733026b6b72f2470

function current_location() => word(0x008A)
location_aliahan = 0x001D
location_aliahan_heros_house = 0x4110
location_aliahan_upstairs_inn = 0x4310
location_aliahan_upstairs_luisa = 0x441E
location_aliahan_castle = 0x461D
location_aliahan_castle_B1 = 0x451F
location_aliahan_castle_2F = 0x471E
location_cave_on_the_promontory = 0x2D13
location_tower_of_najima = 0x3C08
location_tower_of_najima_B1_inn = 0xED0A
location_tower_of_najima_B1 = 0x9F1A
location_tower_of_najima_forest_entrance = 0x9719
location_tower_of_najima_1F = 0xD609
location_tower_of_najima_2F = 0xD709
location_tower_of_najima_3F = 0xD809
location_reeve = 0x091D
location_reeve_upstairs_inn = 0x6D10
location_shrine_southeast_of_reeve = 0x1B07
location_cave_of_enticement_outside = 0x9819
location_cave_of_enticement = 0xA01E
location_cave_of_enticement_B1 = 0xA11E
location_cave_of_enticement_B2 = 0xA21E
location_travel_door_southwest_of_romaly_B1 = 0x9307
location_travel_door_southwest_of_romaly_outside = 0x9219
location_romaly = 0x011D
location_romaly_casino = 0x4807
location_romaly_2F = 0x4A16 
location_romaly_3F_east = 0x9B10
location_romaly_3F_west = 0x4B10
location_romaly_4F_east = 0x4D10
location_romaly_4F_west = 0x4C10
location_kanave = 0x141D
location_kanave_2F = 0x8310
location_noaniels = 0x0B1A
location_noaniels_2F = 0x7710
location_noaniels_upstairs_inn = 0x7810
location_elf_village = 0x8419
location_cave_west_of_noaniels = 0x2E13
location_cave_west_of_noaniels_B2 = 0xA313
location_cave_west_of_noaniels_B3 = 0xA413
location_cave_west_of_noaniels_B4 = 0xA51E
location_tower_of_shanpane = 0x3F08
location_tower_of_shanpane_2F = 0xE809
location_tower_of_shanpane_3F = 0xE909
location_tower_of_shanpane_4F = 0xEB09
location_tower_of_shanpane_5F = 0xEC09
location_tower_of_shanpane_6F = 0xEE09
location_assaram = 0x0C1D
location_assaram_2F = 0x7A07
location_assaram_2FB = 0x7916
location_isis_outside = 0x961E
location_isis = 0x501D
location_isis_casino = 0x8707
location_isis_2F = 0x5110
location_isis_2FB = 0x5210
location_isis_castle = 0x551A
location_isis_castle_2F = 0x5616
location_isis_castle_3F = 0x5710
location_isis_castle_B1 = 0x5407
location_isis_castle_B2 = 0x5308
location_shrine_southeast_of_isis = 0x1E01
location_pyramid = 0x3B0A
location_pyramid_2F = 0xD11B
location_pyramid_3F = 0xD21B
location_pyramid_4F = 0xD31E
location_pyramid_5F = 0xD41E
location_pyramid_6F = 0xD51B
location_pyramid_B1 = 0xD000
location_pyramid_B2 = 0xCF00
location_shrine_northwest_of_romaly = 0x2B1D
location_shrine_northwest_of_romalyB = 0x2C1D
location_shrine_northwest_of_romaly_B1 = 0x9407
location_portoga = 0x0A1D
location_portoga_2F = 0x7010
location_portoga_B1 = 0x6F07
location_portoga_castle = 0x711D
location_shrine_south_of_portoga = 0x1C1C
location_shrine_south_of_portoga_2F = 0x8F16
location_noruds_cave_west = 0x2F1F
location_noruds_cave_east = 0x301F
location_baharata = 0x0E1D
location_baharata_2F = 0x7D10
location_cave_east_of_baharata = 0x3413
location_cave_east_of_baharata_B2 = 0xB213
location_dhama = 0x3313
location_dhama_2F = 0x5C10
location_tower_of_garuna = 0x3D08
location_tower_of_garuna_2F = 0xDB09
location_tower_of_garuna_3F = 0xDC09
location_tower_of_garuna_4F = 0xDD09
location_tower_of_garuna_5F = 0xDE09
location_muor = 0x161D
location_muor_2F = 0x8910
location_tedanki = 0x151D
location_tedanki_2F = 0x8607
location_shrine_north_of_tedanki = 0x1F1D
location_arctic_shrine = 0x271A
location_greenlad = 0x8D1D
location_lancel = 0x0F19
location_lancel_2F = 0x7E10
location_eginbear = 0x021D
location_eginbear_B1 = 0x4E07
location_eginbear_2F = 0x4F16
location_shrine_in_the_shoals = 0x041C
location_tower_of_arp = 0x3E08
location_tower_of_arp_2F = 0xE409
location_tower_of_arp_3F = 0xE509
location_tower_of_arp_4F = 0xE609
location_tower_of_arp_5F = 0xE709
location_shrine_west_of_jipang = 0x241A
location_jipang = 0x171A
location_jipang_B1 = 0x8A07
location_cave_of_jipang = 0x351E
location_cave_of_jipang_B2 = 0xBB1B
location_navel_of_the_earth = 0x3600
location_navel_of_the_earth_B2 = 0xBC13
location_navel_of_the_earth_B3 = 0xBD00
location_house_of_pirates = 0x181D
location_house_of_pirates_B1 = 0x8B07
location_house_of_pirates_B1B = 0x8C07
location_luzami = 0x131D
location_luzami_2F = 0x8210
location_soo = 0x191D
location_merchantville_1 = 0x721D
location_merchantville_2 = 0x731D
location_merchantville_3 = 0x741D
location_merchantville_3_2F = 0x7610
location_merchantville_4 = 0x751D
location_shrine_east_of_samanao = 0x261D
location_samanao = 0x061D
location_samanao_casino = 0x9907
location_samanao_2F = 0x5E10
location_samanao_castle = 0x611D
location_samanao_castle_B1 = 0x6007
location_samanao_castle_B2 = 0x5F07
location_samanao_castle_2F = 0x6216
location_samanao_castle_3F = 0x6316
location_samanao_castle_4F = 0x6416
location_cave_southeast_of_samanao = 0x371E
location_cave_southeast_of_samanao_B2 = 0xBE13
location_cave_southeast_of_samanao_B3 = 0xBF13
location_shrine_of_the_world_tree = 0x2216
location_shrine_of_olivia = 0x211D
location_phantom_ship = 0x9C1C
location_phantom_ship_B1 = 0x9D1C
location_shrine_jail = 0x1D07
location_cave_of_necrogond = 0x321E
location_cave_of_necrogond_2F = 0xA81E
location_cave_of_necrogond_3F = 0xA91E
location_cave_of_necrogond_4F = 0xAA1E
location_cave_of_necrogond_5F = 0x311E
location_shrine_of_necrogond = 0x2019
location_shrine_of_liamland = 0x2516
location_castle_baramos = 0x031D
location_castle_baramos_2F = 0x5A16
location_castle_baramos_B1 = 0x5908
location_castle_baramos_B2 = 0x5813
location_castle_of_the_dragon_queen = 0x231D
location_pit_of_giaga_before = 0xC61E
location_pit_of_giaga = 0xA61E
location_pit_of_giaga_harbor = 0x911C
location_tantegel = 0x071D
location_tantegel_2F = 0x6610
location_tantegel_2FB = 0x6510
location_tantegel_castle = 0x691D
location_tantegel_castle_2F = 0x6B1F
location_tantegel_castle_2FB = 0x6C10
location_tantegel_castle_2FC = 0x6A16
location_tantegel_castle_B1 = 0x6807
location_cave_northwest_of_tantegel = 0x381E
location_cave_northwest_of_tantegel_B2 = 0xC31E
location_cave_northwest_of_tantegel_B3 = 0xC51E
location_cave_southwest_of_tantegel = 0x391E
location_cave_southwest_of_tantegel_B2 = 0xA71E
location_garins_house = 0x281A
location_garins_house_B1 = 0x9007
location_kol = 0x1A19
location_kol_2F = 0x8E10
location_shrine_of_honor = 0x2A10
location_shrine_of_rain = 0x2910
location_hauksness = 0x121E
location_cantlin = 0x101D
location_cantlin_2F = 0x7F10
location_cantlin_casino = 0x9A07
location_swamp_cave = 0x3A07
location_rimuldar = 0x111D
location_rimuldar_2F = 0x8010
location_tower_west_of_kol = 0x4008
location_tower_west_of_kol_2F = 0xF009
location_tower_west_of_kol_3F = 0xF109
location_tower_west_of_kol_4F = 0xF209
location_tower_west_of_kol_5F = 0xEF09
location_castle_zoma = 0x080A
location_castle_zoma_B1 = 0xC800
location_castle_zoma_B2 = 0xC91E
location_castle_zoma_B3 = 0xCA13
location_castle_zoma_B4 = 0xCB03
location_castle_zoma_B5 = 0xCC13

locations = {
    location_aliahan: "Aliahan",
    location_aliahan_heros_house: "Aliahan",
    location_aliahan_upstairs_inn: "Aliahan",
    location_aliahan_upstairs_luisa: "Aliahan",
    location_aliahan_castle: "Aliahan",
    location_aliahan_castle_B1: "Aliahan",
    location_aliahan_castle_2F: "Aliahan",
    location_cave_on_the_promontory: "the Cave on the Promontory",
    location_tower_of_najima: "the Tower of Najima",
    location_tower_of_najima_B1_inn: "the Tower of Najima",
    location_tower_of_najima_B1: "the Tower of Najima",
    location_tower_of_najima_1F: "the Tower of Najima",
    location_tower_of_najima_2F: "the Tower of Najima",
    location_tower_of_najima_3F: "the Tower of Najima",
    location_tower_of_najima_forest_entrance: "the forest near Reeve",
    location_reeve: "Reeve",
    location_reeve_upstairs_inn: "Reeve",
    location_shrine_southeast_of_reeve: "the Shrine southeast of Reeve",
    location_cave_of_enticement_outside: "the Cave of Enticement",
    location_cave_of_enticement: "the Cave of Enticement",
    location_cave_of_enticement_B1: "the Cave of Enticement",
    location_cave_of_enticement_B2: "the Cave of Enticement",
    location_travel_door_southwest_of_romaly_outside: "the travel door southwest of Romaly",
    location_travel_door_southwest_of_romaly_B1: "the travel door southwest of Romaly",
    location_romaly: "Romaly",
    location_romaly_casino: "Romaly",
    location_romaly_2F: "Romaly",
    location_romaly_3F_east: "Romaly",
    location_romaly_3F_west: "Romaly",
    location_romaly_4F_east: "Romaly",
    location_romaly_4F_west: "Romaly",
    location_kanave: "Kanave",
    location_kanave_2F: "Kanave",
    location_noaniels: "Noaniels",
    location_noaniels_2F: "Noaniels",
    location_noaniels_upstairs_inn: "Noaniels",
    location_elf_village: "the Elf Village",
    location_cave_west_of_noaniels: "the Cave west of Noaniels",
    location_cave_west_of_noaniels_B2: "the Cave west of Noaniels",
    location_cave_west_of_noaniels_B3: "the Cave west of Noaniels",
    location_cave_west_of_noaniels_B4: "the Cave west of Noaniels",
    location_tower_of_shanpane: "the Tower of Shanpane",
    location_tower_of_shanpane_2F: "the Tower of Shanpane",
    location_tower_of_shanpane_3F: "the Tower of Shanpane",
    location_tower_of_shanpane_4F: "the Tower of Shanpane",
    location_tower_of_shanpane_5F: "the Tower of Shanpane",
    location_tower_of_shanpane_6F: "the Tower of Shanpane",
    location_assaram: "Assaram",
    location_assaram_2F: "Assaram",
    location_assaram_2FB: "Assaram",
    location_isis_outside: "Isis",
    location_isis: "Isis",
    location_isis_casino: "Isis",
    location_isis_2F: "Isis",
    location_isis_2FB: "Isis",
    location_isis_castle: "Isis",
    location_isis_castle_2F: "Isis",
    location_isis_castle_3F: "Isis",
    location_isis_castle_B1: "Isis",
    location_isis_castle_B2: "Isis",
    location_shrine_southeast_of_isis: "the Shrine southeast of Isis",
    location_pyramid: "the Pyramid",
    location_pyramid_2F: "the Pyramid",
    location_pyramid_3F: "the Pyramid",
    location_pyramid_4F: "the Pyramid",
    location_pyramid_5F: "the Pyramid",
    location_pyramid_6F: "the Pyramid",
    location_pyramid_B1: "the Pyramid",
    location_pyramid_B2: "the Pyramid",
    location_shrine_northwest_of_romaly: "the Shrine northwest of Romaly",
    location_shrine_northwest_of_romalyB: "the Shrine northwest of Romaly",
    location_shrine_northwest_of_romaly_B1: "the Shrine northwest of Romaly", // final key door here
    location_portoga: "Portoga",
    location_portoga_2F: "Portoga",
    location_portoga_B1: "Portoga",
    location_portoga_castle: "Portoga",
    location_shrine_south_of_portoga: "the Shrine south of Portoga",
    location_shrine_south_of_portoga_2F: "the Shrine south of Portoga",
    location_noruds_cave_west: "Norud's Cave",
    location_noruds_cave_east: "Norud's Cave",
    location_baharata: "Baharata",
    location_baharata_2F: "Baharata",
    location_cave_east_of_baharata: "the Cave east of Baharata",
    location_cave_east_of_baharata_B2: "the Cave east of Baharata",
    location_dhama: "the Shrine of Dhama",
    location_dhama_2F: "the Shrine of Dhama",
    location_tower_of_garuna: "the Tower of Garuna",
    location_tower_of_garuna_2F: "the Tower of Garuna",
    location_tower_of_garuna_3F: "the Tower of Garuna",
    location_tower_of_garuna_4F: "the Tower of Garuna",
    location_tower_of_garuna_5F: "the Tower of Garuna",
    location_muor: "Muor",
    location_muor_2F: "Muor",
    location_tedanki: "Tedanki",
    location_tedanki_2F: "Tedanki",
    location_shrine_north_of_tedanki: "the Shrine north of Tedanki",
    location_arctic_shrine: "the Arctic Shrine",
    location_greenlad: "Greenlad",
    location_lancel: "Lancel",
    location_lancel_2F: "Lancel",
    location_eginbear: "Eginbear",
    location_eginbear_B1: "Eginbear",
    location_eginbear_2F: "Eginbear",
    location_shrine_in_the_shoals: "the Shrine in the Shoals",
    location_tower_of_arp: "the Tower of Arp",
    location_tower_of_arp_2F: "the Tower of Arp",
    location_tower_of_arp_3F: "the Tower of Arp",
    location_tower_of_arp_4F: "the Tower of Arp",
    location_tower_of_arp_5F: "the Tower of Arp",
    location_jipang: "Jipang",
    location_jipang_B1: "Jipang",
    location_shrine_west_of_jipang: "the Shrine west of Jipang",
    location_cave_of_jipang: "the Cave of Jipang",
    location_cave_of_jipang_B2: "the Cave of Jipang",
    location_navel_of_the_earth: "the Navel of the Earth",
    location_navel_of_the_earth_B2: "the Navel of the Earth",
    location_navel_of_the_earth_B3: "the Navel of the Earth",
    location_house_of_pirates: "the House of Pirates",
    location_house_of_pirates_B1: "the House of Pirates",
    location_house_of_pirates_B1B: "the House of Pirates",
    location_luzami: "Luzami",
    location_luzami_2F: "Luzami",
    location_soo: "Soo",
    location_merchantville_1: "the Merchant Village",
    location_merchantville_2: "the Merchant Village",
    location_merchantville_3: "the Merchant Village",
    location_merchantville_3_2F: "the Merchant Village",
    location_merchantville_4: "the Merchant Village",
    location_shrine_east_of_samanao: "the Shrine east of Samanao",
    location_samanao: "Samanao",
    location_samanao_casino: "Samanao",
    location_samanao_2F: "Samanao",
    location_samanao_castle: "Samanao",
    location_samanao_castle_B1: "Samanao",
    location_samanao_castle_B2: "Samanao",
    location_samanao_castle_2F: "Samanao",
    location_samanao_castle_3F: "Samanao",
    location_samanao_castle_4F: "Samanao",
    location_cave_southeast_of_samanao: "the Cave southeast of Samanao",
    location_cave_southeast_of_samanao_B2: "the Cave southeast of Samanao",
    location_cave_southeast_of_samanao_B3: "the Cave southeast of Samanao",
    location_shrine_of_the_world_tree: "the Great Forest Shrine",
    location_shrine_of_olivia: "the Shrine at the promontory of Olivia",
    location_phantom_ship: "the Phantom Ship",
    location_phantom_ship_B1: "the Phantom Ship",
    location_shrine_jail: "the Shrine Jail",
    location_cave_of_necrogond: "the Cave to Necrogond",
    location_cave_of_necrogond_2F: "the Cave to Necrogond",
    location_cave_of_necrogond_3F: "the Cave to Necrogond",
    location_cave_of_necrogond_4F: "the Cave to Necrogond",
    location_cave_of_necrogond_5F: "the Cave to Necrogond",
    location_shrine_of_necrogond: "the Shrine of Necrogond",
    location_shrine_of_liamland: "the Shrine of Liamland",
    location_castle_baramos: "Baramos's Castle",
    location_castle_baramos_2F: "Baramos's Castle",
    location_castle_baramos_B1: "Baramos's Castle",
    location_castle_baramos_B2: "Baramos's Castle",
    location_castle_of_the_dragon_queen: "the Castle of the Dragon Queen",
    location_pit_of_giaga_before: "the Pit of Giaga",
    location_pit_of_giaga: "the Pit of Giaga",
    location_pit_of_giaga_harbor: "the Pit of Giaga",
    location_tantegel: "Tantegel",
    location_tantegel_2F: "Tantegel",
    location_tantegel_2FB: "Tantegel",
    location_tantegel_castle: "Tantegel",
    location_tantegel_castle_2F: "Tantegel",
    location_tantegel_castle_2FB: "Tantegel",
    location_tantegel_castle_2FC: "Tantegel",
    location_tantegel_castle_B1: "Tantegel",
    location_cave_northwest_of_tantegel: "the Cave northwest of Tantegel",
    location_cave_northwest_of_tantegel_B2: "the Cave northwest of Tantegel",
    location_cave_northwest_of_tantegel_B3: "the Cave northwest of Tantegel",
    location_cave_southwest_of_tantegel: "the Cave southwest of Tantegel",
    location_cave_southwest_of_tantegel_B2: "the Cave southwest of Tantegel",
    location_garins_house: "Garin's House",
    location_garins_house_B1: "Garin's House",
    location_kol: "Kol",
    location_kol_2F: "Kol",
    location_shrine_of_honor: "the Shrine of Honor",
    location_shrine_of_rain: "the Shrine of Rain",
    location_hauksness: "Hauksness",
    location_cantlin: "Cantlin",
    location_cantlin_2F: "Cantlin",
    location_cantlin_casino: "Cantlin",
    location_swamp_cave: "the Swamp Cave",
    location_rimuldar: "Rimuldar",
    location_rimuldar_2F: "Rimuldar",
    location_tower_west_of_kol: "the Tower west of Kol",
    location_tower_west_of_kol_2F: "the Tower west of Kol",
    location_tower_west_of_kol_3F: "the Tower west of Kol",
    location_tower_west_of_kol_4F: "the Tower west of Kol",
    location_tower_west_of_kol_5F: "the Tower west of Kol",
    location_castle_zoma: "Zoma's Castle",
    location_castle_zoma_B1: "Zoma's Castle",
    location_castle_zoma_B2: "Zoma's Castle",
    location_castle_zoma_B3: "Zoma's Castle",
    location_castle_zoma_B4: "Zoma's Castle",
    location_castle_zoma_B5: "Zoma's Castle",
}

function is_on_world_map() => byte(0x002F) == 0
function is_on_dark_world_map() => byte(0x002F) == 2
function is_in_combat() => byte(0x0032) == 0xFD

function enemy_type(group_id) => byte(0x056D + group_id)
function enemy_hp(slot) => word(0x0500 + slot * 2)
function enemy_alive(slot) => bit7(0x0531 + slot * 2) == 1

function world_x() => byte(0x0100)
function world_y() => byte(0x0101)

function is_in_game() => byte(0x0102) == 0 && byte(0x6000) == 0 && character_level(0) > 0

function character_level(index) => byte(0x0700 + index)
function character_class(index) => byte(0x0718 + index)
function character_gender(index) => bit3(0x0718 + index)

class_hero = 0	
class_wizard = 1	
class_pilgrim = 2	
class_sage = 3	
class_soldier = 4	
class_merchant = 5	
class_fighter = 6	
class_goof_off = 7

classes = {
    class_hero: "hero",
    class_wizard: "wizard",
    class_pilgrim: "pilgrim",
    class_sage: "sage",
    class_soldier: "soldier",
    class_merchant: "merchant",
    class_fighter: "fighter",
    class_goof_off: "goof-off"
}

// ===========================

function story_achievement(bit, points, location, title, description)
{
    achievement(
        title = title,
        description = description,
        points = points,
        trigger = is_in_game() && current_location() == location && bit == 1 && prev(bit) != 1)
}

function item_achievement(item_id, points, location_trigger, title, description)
{
    trigger = always_false()

    // US ROM has inventory from 0x77C-0x79B (8 bytes per character)
    // JP ROM has inventory from 0x76C-0x78B (8 bytes per character)
    // as such, the middle 16 bytes are shared, but we have to include the outer 16 bytes on each end to
    // support both. 0x76C-0x77B are name bytes in the US ROM (US names use 8 bytes each, JP only 4 bytes each),
    // so they won't be changing from 0xFF to the item and are safe to use. 0x78C-0x79B are learned spells,
    // so they won't be changing when acquiring the items either.
    for addr in range(0x076C, 0x079B)
        trigger = trigger || (byte(addr) == item_id && prev(byte(addr)) == 0xFF)

    achievement(
        title = title,
        description = description,
        points = points,
        trigger = is_in_game() && location_trigger && trigger)
}

function achievement_world_location(title, description, points, x, y)
{
    achievement(
        title = title,
        description = description,
        points = points,
        trigger = world_x() == x && world_y() == y && is_on_world_map() && character_level(0) > 0)
}

function story_achievement_world_location(bit, points, x, y, title, description)
{
    achievement(
        title = title,
        description = description,
        points = points,
        trigger = is_in_game() && world_x() == x && world_y() == y && is_on_world_map() && bit == 1 && prev(bit) != 1)
}

function story_achievement_dark_world_location(bit, points, x, y, title, description)
{
    achievement(
        title = title,
        description = description,
        points = points,
        trigger = is_in_game() && world_x() == x && world_y() == y && is_on_dark_world_map() && bit == 1 && prev(bit) != 1)
}

function achievement_enemy_killed(title, description, points, enemy_id) // only works if enemy is in a group by itself
{
    achievement(
        title = title,
        description = description,
        points = points,
        trigger = enemy_type(0) == enemy_id && enemy_hp(0) == 0 && prev(enemy_hp(0)) > 0
    )
}

// byte(0x6acb) seems to somehow be related to the window that's focused
// 0x00 = blank window (or no window)
// 0x01 = basic battle window (seems to be used for attacks)
// 0x02 = basic battle window (seems to be used for spells and items)
// 0x13 = battle target window or item select window
// 0x14 = battle command window or party member target window
// 0x18 = battle wizard/pilgrim spell selector or use/equip window
function trigger_battle_playing_out() => (byte(0x6acb) > 0 && byte(0x6acb) < 3)

function achievement_enemy_killed_group(title, description, points, enemy_id)
{
    trigger = never(!is_in_combat())                 // force reset if player dies after killing babble
    for i in range(0,7)
        trigger = trigger && !enemy_alive(i)         // battle is over when no more enemies are left

    trigger_alt = (once(byte(0x0059) == enemy_id && // type of monster defeated
                        trigger_battle_playing_out() &&
                        byte(0x0072) == 0x48)) ||   // "X is defeated!"
                  (once(byte(0x0059) == enemy_id && // type of monster defeated
                        trigger_battle_playing_out() &&
                        byte(0x0072) == 0x5D))      // "You have defeated X" - poison needle message is slightly different
    
    achievement(id=50424,
        title = title,
        description = description,
        points = points,
        trigger = trigger && trigger_alt
    )
}

function achievement_all_chests(title, description, points, chests)
{
    trigger = is_in_game()
    trigger_or = always_false()
    
    for pair in chests
    {
        location = pair[0]
        bit = pair[1]
        
        trigger = trigger && bit == 1
        trigger_or = trigger_or || (current_location() == location && prev(bit) == 0)
    }
    
    achievement(title = title, description = description, points = points, 
               trigger = trigger && trigger_or)
}

// ===========================
// 00 - New Game

achievement(
    title = "With a Little Help from My Friends",
    description = "Build a full party",
    points = 1,
    trigger = is_in_game() && character_level(3) > 0
)

// ===========================
// 01 - Tower of Najima (Lv4)

story_achievement(bit1(0x60BF), 5, location_tower_of_najima_3F, "Now You Can Open Doors", "Get the Thief's Key")

// ===========================
// 02 - Cave of Enticement (Lv9)

achievement_world_location("The Adventure Begins", "Reach the main continent", 10, 49, 93)

// ===========================
// 03 - Dream Ruby (Lv11)

story_achievement(bit2(0x608F), 4, location_kanave, "Something from the Back Room", "Get the Poison Needle from Kanave")
story_achievement(bit2(0x60B7), 10, location_noaniels, "Rise and Shine", "Wake up the residents of Noaniels")

// ===========================
// 04 - Kandar (Lv13)

// Kandar fight: First round: Hero attack, Pilgrim SpeedUp, Goof-off attack, Wizard increase. 
//               Future rounds: Hero attack, Pilgrim Heal or Sap, Goof-off attack, Wizard Bang

story_achievement(bit6(0x60B7), 5, location_romaly_2F, "It's Good to Be the King", "Become royalty")
story_achievement(bit4(0x60BA), 5, location_assaram_2F, "Fortune Favors the Bold", "Enjoy the nightlife of Assaram")

// ===========================
// 05 - Isis (Lv16)

story_achievement(bit2(0x6094), 4, location_isis_castle_B2, "I Didn't See Your Name on It", "Find the hidden item beneath Isis")
story_achievement(bit0(0x609F), 10, location_pyramid_3F, "You Can Open More Doors", "Get the Magic Key")

achievement_all_chests("Archaeologist", "Open all 24 chests in the pyramid", 10, [
    [location_pyramid,    bit5(0x6092)], // NW chest 1/3 1F (empty)
    [location_pyramid,    bit4(0x6092)], // NW chest 2/3 1F (empty)
    [location_pyramid,    bit3(0x6092)], // NW chest 3/3 1F (empty)
    [location_pyramid,    bit2(0x6092)], // W chest 1/3 1F (empty)
    [location_pyramid,    bit1(0x6092)], // W chest 2/3 1F (man-eater)
    [location_pyramid,    bit0(0x6092)], // W chest 3/3 1F (man-eater)
    [location_pyramid,    bit7(0x6093)], // SW chest 1F (empty)
    [location_pyramid,    bit6(0x6093)], // SE chest 1F (empty)
    [location_pyramid_2F, bit2(0x609F)], // SW chest 2F (empty)
    [location_pyramid_3F, bit1(0x609F)], // N chest 1/2 3F (vitality seed)
    [location_pyramid_3F, bit0(0x609F)], // N chest 2/2 3F (magic key)
    [location_pyramid_4F, bit7(0x60A0)], // chest 1/12 4F (176G)
    [location_pyramid_4F, bit6(0x60A0)], // chest 2/12 4F (40G)
    [location_pyramid_4F, bit5(0x60A0)], // chest 3/12 4F (strength seed)
    [location_pyramid_4F, bit4(0x60A0)], // chest 4/12 4F (80G)
    [location_pyramid_4F, bit3(0x60A0)], // chest 12/12 4F (24G)
    [location_pyramid_4F, bit2(0x60A0)], // chest 5/12 4F (agility seed)
    [location_pyramid_4F, bit1(0x60A0)], // chest 11/12 4F (medical herb)
    [location_pyramid_4F, bit0(0x60A0)], // chest 6/12 4F (15G)
    [location_pyramid_4F, bit7(0x60A1)], // chest 10/12 4F (56G)
    [location_pyramid_4F, bit6(0x60A1)], // chest 9/12 4F (wing of wyvern)
    [location_pyramid_4F, bit5(0x60A1)], // chest 8/12 4F (304G)
    [location_pyramid_4F, bit4(0x60A1)], // chest 7/12 4F (24G)
    [location_pyramid_5F, bit3(0x60A1)], // chest 5F (flashy clothes)
])

story_achievement(bit1(0x60CA), 10, location_pyramid_B2, "Tomb Raider", "Find the hidden item beneath the pyramid")
story_achievement(bit0(0x60A7), 4, location_isis_castle_3F, "Drop Something?", "Find the Wizard's Ring in the queen's chambers in Isis")

// ===========================
// 06 - Pepper (Lv17)

story_achievement(bit5(0x60B6), 5, location_noruds_cave_west, "Bolef's Path", "Convince Norud to show you the secret passage through the mountains")
story_achievement(bit7(0x60BF), 10, location_baharata, "Achoo!", "Get the Black Pepper")

// ===========================
// 07 - Green Orb (Lv18)

story_achievement(bit6(0x6097), 4, location_tedanki_2F, "Night Time Anytime", "Get the Lamp of Darkness")
story_achievement(bit3(0x6094), 5, location_eginbear_B1, "Boulder Mover", "Get the Vase of Drought")
story_achievement(bit4(0x60A7), 4, location_soo, "Who Left That There", "Find the Staff of Thunder in Soo")
story_achievement(bit3(0x608E), 10, location_shrine_in_the_shoals, "You Can Open All Doors", "Get the Final Key")
story_achievement(bit5(0x60CE), 10, location_tedanki, "The Prisoner's Last Possession", "Get the Green Orb")

// ===========================
// 08 - Dhama (Lv19)

function character_class_changed(index) 
{
    name_not_blank = byte(0x075C + 8 * index) != 0
    name_front     = dword(0x075C + 8 * index)      // first four bytes of name
    name_back      = dword(0x0760 + 8 * index)      // last four bytes of name
    
    return character_class(index) != prev(character_class(index)) &&              // class changed
           character_level(index) == 1 && prev(character_level(index)) >= 20 &&   // level went from 20+ to 1
           name_front == prev(name_front) && name_back == prev(name_back) &&      // name didn't change (prevent trigger when swapping out at luisa's)
           name_not_blank                                                         // name provided
}

achievement(
     title = "A New Line of Work",
     description = "Have one of your characters change professions",
     points = 5,
     trigger = is_in_game() && current_location() == location_dhama &&
               (character_class_changed(0) || character_class_changed(1) ||
                character_class_changed(2) || character_class_changed(3))
)

item_water_blaster = 0x6D
item_achievement(item_water_blaster, 4, current_location() == location_muor_2F, "A Child's Toy", "Get the Water Blaster")

story_achievement(bit7(0x60A2), 10, location_tower_of_garuna_2F, "Required Reading", "Get the Book of Satori")

// ===========================
// 09 - Tower of Arp (Lv21)

story_achievement(bit3(0x60A2), 4, location_tower_of_arp_3F, "Sonic Finder", "Get the Echoing Flute")

// ===========================
// 10 - Purple Orb (Lv23 - wizard gets bikill at 23)

story_achievement(bit3(0x60CE), 10, location_jipang, "The Man with Many Heads", "Get the Purple Orb")

// ===========================
// 11 - Blue Orb (Lv24)

story_achievement(bit4(0x60CE), 10, location_navel_of_the_earth_B3, "All By Myself", "Get the Blue Orb")
story_achievement(bit1(0x60CE), 5, location_house_of_pirates_B1, "Buried Treasure", "Get the Red Orb")

story_achievement(bit1(0x60A7), 4, location_luzami, "In Memoriam", "Find the Intelligence Seed in Luzami")

// ===========================
// 12 - False King (Lv26)

achievement_all_chests("Spelunker", "Open all 23 chests in the cave near Samanao", 10, [
    [location_cave_southeast_of_samanao_B2, bit5(0x609B)], // B2-J - mimic
    [location_cave_southeast_of_samanao_B2, bit4(0x609B)], // B2-K - mimic
    [location_cave_southeast_of_samanao_B2, bit3(0x609B)], // B2-G - acorns of life
    [location_cave_southeast_of_samanao_B2, bit2(0x609B)], // B2-H - medical herb
    [location_cave_southeast_of_samanao_B2, bit1(0x609B)], // B2-I - 320g
    [location_cave_southeast_of_samanao_B2, bit0(0x609B)], // B2-L - mimic
    [location_cave_southeast_of_samanao_B2, bit7(0x609C)], // B2-M - mimic
    [location_cave_southeast_of_samanao_B2, bit6(0x609C)], // B2-F - 24g
    [location_cave_southeast_of_samanao_B2, bit5(0x609C)], // B2-A - 128g
    [location_cave_southeast_of_samanao_B2, bit4(0x609C)], // B2-E - 568g
    [location_cave_southeast_of_samanao_B2, bit3(0x609C)], // B2-C - 56g
    [location_cave_southeast_of_samanao_B2, bit2(0x609C)], // B2-B - strength seed
    [location_cave_southeast_of_samanao_B2, bit1(0x609C)], // B2-D - wing of wyvern
    [location_cave_southeast_of_samanao_B2, bit0(0x609C)], // B2-Q - mimic
    [location_cave_southeast_of_samanao_B2, bit7(0x609D)], // B2-R - mimic
    [location_cave_southeast_of_samanao_B2, bit6(0x609D)], // B2-T - mimic
    [location_cave_southeast_of_samanao_B2, bit5(0x609D)], // B2-U - stone of life
    [location_cave_southeast_of_samanao_B2, bit4(0x609D)], // B2-P - medical herb
    [location_cave_southeast_of_samanao_B2, bit3(0x609D)], // B2-O - vitality seed
    [location_cave_southeast_of_samanao_B2, bit2(0x609D)], // B2-N - 24g
    [location_cave_southeast_of_samanao_B2, bit1(0x609D)], // B2-S - mimic
    [location_cave_southeast_of_samanao_B3, bit0(0x609D)], // B3-W - mirror of ra
    [location_cave_southeast_of_samanao_B3, bit7(0x609E)]  // B3-V - animal suit
])

story_achievement(bit1(0x60A5), 10, location_samanao_castle_2F, "The False King", "Get the Staff of Change")

// ===========================
// 13 - Yellow Orb (Lv27)

story_achievement(bit2(0x60CE), 10, location_merchantville_4, "The Merchantville Rebellion", "Get the Yellow Orb")

item_leaf_of_the_world_tree = 0x69
item_achievement(item_leaf_of_the_world_tree, 5, world_x() == 0x7B && world_y() == 0x4C && is_on_world_map(), 
                 "Lost in the Forest", "Get a Leaf from the World Tree")

story_achievement_world_location(bit3(0x60B6), 10, 0x4A, 0x90, "Land Bridge", "Set off a volcano")

// ===========================
// 14 - Silver Orb (Lv32)

story_achievement(bit0(0x60CE), 10, location_shrine_of_necrogond, "The Hidden Valley", "Get the Silver Orb")
story_achievement(bit6(0x60B9), 5, location_shrine_of_liamland, "The Legendary Phoenix", "Hatch the giant egg in Liamland")

// ===========================
// 15 - Baramos (Lv35)

story_achievement(bit7(0x60CB), 25, location_castle_baramos_B2, "The Archfiend is Vanquished!", "Defeat Baramos")

// ===========================
// 16 - Dark World (Lv35)

story_achievement(bit4(0x60BF), 4, location_portoga, "Made for a Woman", "Get the Sword of Illusion")
story_achievement(bit2(0x6097), 4, location_garins_house_B1, "Music for Monsters", "Get the Silver Harp")
story_achievement(bit6(0x609E), 4, location_cave_northwest_of_tantegel_B3, "Heroic Blocker", "Get the Shield of Heroes")

item_sword_of_kings = 0x1C
item_achievement(item_sword_of_kings, 10, current_location() == location_kol_2F, "Forged", "Get the Sword of Kings")

story_achievement(bit5(0x60BA), 5, location_tower_west_of_kol_5F, "The Statue Awakens", "Get the Sacred Amulet")
story_achievement_dark_world_location(bit7(0x60B6), 5, 0x54, 0x3A, "It Only Takes One Drop", "Build the rainbow bridge")

// ===========================
// 17 - Zoma (Lv45)

//story_achievement(bit5(0x609F), 8, location_castle_zoma_B4, "Pocket Healer", "Get the Sage's Stone")

// Have fastest character use the Sphere of Light in the first round, and everyone else parry. The weakest
// character should use the Sage's Stone every round. Other characters should use their most powerful attacks.
// The Hero should use Lightning, a Wizard/Sage should use Explodet, Soldiers and Fighters should attack.
// If you have a weaker character with Bikill, you can try to put it on your melee characters, but Zoma will
// usually nullify it quickly - which does make him waste one of this two turns. He only has 1000 HP, but
// regenerates roughly 100 HP every round.
achievement(
    title = "The Foretelling",
    description = "Defeat Zoma",
    points = 25,
    trigger = is_in_game() && current_location() == location_castle_zoma_B5 && byte(0x60C5) == 0xC0
)

// Suggestion for this achievement: Lightning is still the best thing the Hero can do. The Sage should be 
// equipped with the Meteorite Armband, and should cast Barrier whenever it's down, then either Sap or 
// provide secondary healing. A third character should use the Sage's Stone every round. The fourth character 
// should use their most damaging attacks. He still only has 1000 HP and recovers about 100 HP per round, but
// has much higher physical defense. Depending on party composition, you should be able to do this at Lv~47.
// When using the Sphere of Light, the active enemy id changes from 133 to 134.
achievement_enemy_killed("We're Not Scared", "Defeat Zoma without using the Sphere of Light", 25, 133)

achievement(
    title = "A Legend is Born",
    description = "See the epilogue",
    points = 5,
    trigger = byte(0x60C5) == 0 && prev(byte(0x60C5)) == 160
)

// ===========================
// Miscellaneous

function arena_battle_in_progress() => byte(0x6A64)
function arena_actual_winner() => byte(0x6A65)
function arena_selected_winner() => byte(0x6A67)
achievement(
    title = "Winner!",
    description = "Pick the winning monster in a stadium battle",
    points = 5,
    trigger = arena_battle_in_progress() == 0 && prev(arena_battle_in_progress()) == 128 && // as the battle finishes
              once(arena_selected_winner() == arena_actual_winner()) &&
              never(arena_battle_in_progress() == 128) // prevents triggering if previous winner matches newly selected winner
)

enemy_metal_babble = 0x6C
achievement_enemy_killed_group("Slippery Experience", "Defeat a Metal Babble", 5, enemy_metal_babble)

spell_blazemore = 1
spell_healmore = 0x1B
spell_bikill = 0x32
action_spell_enemy = 9
action_spell_ally = 1

function in_battle() => byte(0x0032) == 0xFD
function attacker_index() => byte(0x0051)
function attacker_action(index) => high4(0x054C + index)
function attacker_spell(index) => byte(0x0558 + index)

function multi_class_test(index, class, spell, action)
{
    return (attacker_index() == index && character_class(index) == class && attacker_action(index) == action && attacker_spell(index) == spell)
}

function multi_class_achievement(title, description, points, class, spell, action)
{
    achievement(title = title, description = description, points = points,
                trigger = in_battle() && 
                          (multi_class_test(0, class, spell, action) || 
                           multi_class_test(1, class, spell, action) || 
                           multi_class_test(2, class, spell, action) || 
                           multi_class_test(3, class, spell, action) ||
                           multi_class_test(0, class + 8, spell, action) || 
                           multi_class_test(1, class + 8, spell, action) || 
                           multi_class_test(2, class + 8, spell, action) || 
                           multi_class_test(3, class + 8, spell, action)))
}

// pilgrim (vivify:24) -> wizard/sage (bikill:23) -> soldier = sturdy healer

// wizard (blazemore:17) -> soldier
multi_class_achievement("Mage Knight", "Cast Blazemore as a soldier", 10, class_soldier, spell_blazemore, action_spell_enemy)

// pilgrim (healmore:14) -> soldier
multi_class_achievement("Paladin", "Cast Healmore (in battle) as a soldier", 10, class_soldier, spell_healmore, action_spell_ally)

// wizard (bikill:23) -> fighter
multi_class_achievement("Berserker", "Cast Bikill as a fighter", 10, class_fighter, spell_bikill, action_spell_ally)

achievement(
    title = "So Much Magic", 
    description = "Have two sages in your party", 
    points = 10, 
    trigger = never(character_class(0) != prev(character_class(0))) &&
              never(character_class(1) != prev(character_class(1))) &&
              never(character_class(2) != prev(character_class(2))) &&
              never(character_class(3) != prev(character_class(3))) &&
              tally(2, once(character_class(0) == class_sage) ||
                       once(character_class(0) == class_sage + 8) ||
                       once(character_class(1) == class_sage) ||
                       once(character_class(1) == class_sage + 8) ||
                       once(character_class(2) == class_sage) ||
                       once(character_class(2) == class_sage + 8) ||
                       once(character_class(3) == class_sage) ||
                       once(character_class(3) == class_sage + 8))
)

// + repurpose gambling achievement (1x)
// ? find yayoi hiding in jipang (missable)
// ? get the intelligence seed from luzami
// ? revisit the elf village as elves [buy a wizard ring] (missble)
// ? get the sage's stone from zoma's castle

// NOTE: GBC version has blasphemous warning using LOTO as name

// ===========================

// 0-7 are male, 8-15 are female
for key in classes {
    classes[key + 8] = classes[key]
}

pronouns = {
    0: "his",
    1: "her"
}

rich_presence_conditional_display(dword(0x0100) == 0xFFFFFFFF, "The quest has not yet begun")

rich_presence_conditional_display(character_level(1) == 0 && is_on_world_map(), "A level {0} {1} is exploring the world",
    rich_presence_value("Level", character_level(0)),
    rich_presence_lookup("Class", character_class(0), classes)
)

rich_presence_conditional_display(is_on_world_map(), "A level {0} {1} and {2} companions are exploring the world",
    rich_presence_value("Level", character_level(0)),
    rich_presence_lookup("Class", character_class(0), classes),
    rich_presence_lookup("Pronoun", character_gender(0), pronouns)
)

rich_presence_conditional_display(character_level(1) == 0 && is_on_dark_world_map(), "A level {0} {1} is exploring the world of darkness",
    rich_presence_value("Level", character_level(0)),
    rich_presence_lookup("Class", character_class(0), classes)
)

rich_presence_conditional_display(is_on_dark_world_map(), "A level {0} {1} and {2} companions are exploring the world of darkness",
    rich_presence_value("Level", character_level(0)),
    rich_presence_lookup("Class", character_class(0), classes),
    rich_presence_lookup("Pronoun", character_gender(0), pronouns)
)

rich_presence_conditional_display(character_level(1) == 0, "A level {0} {1} is exploring {3}",
    rich_presence_value("Level", character_level(0)),
    rich_presence_lookup("Class", character_class(0), classes),
    rich_presence_lookup("Pronoun", character_gender(0), pronouns),
    rich_presence_lookup("Location", current_location(), locations)
)

rich_presence_display("A level {0} {1} and {2} companions are exploring {3}",
    rich_presence_value("Level", character_level(0)),
    rich_presence_lookup("Class", character_class(0), classes),
    rich_presence_lookup("Pronoun", character_gender(0), pronouns),
    rich_presence_lookup("Location", current_location(), locations)
)
